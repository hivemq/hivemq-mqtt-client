buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: '1.0.3'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.12.1'
    id 'net.ltgt.apt' version '0.10'
    id "com.github.johnrengelman.shadow" version "2.0.3"
    id 'maven-publish'
    id "com.github.jk1.dependency-license-report" version "0.6.1"
}

apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'idea'
apply plugin: 'eclipse'

group 'org.mqtt-bee'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

project.ext {
    rxJavaVersion = '2.1.12'
    nettyVersion = '4.1.22.Final'
    daggerVersion = '2.15'
    guavaVersion = '24.1-jre'
    logbackVersion = '1.2.3'
    jcToolsVersion = '2.1.1'
    junit4Version = '4.12'
    junitVintageVersion = '4.12.3'
    junitJupiterVersion = '5.0.3'
    junitPlatformVersion = '1.0.3'
}

configurations.all {
    resolutionStrategy.force "com.google.guava:guava:${guavaVersion}"
    resolutionStrategy.force "org.checkerframework:checker-compat-qual:2.3.0"
}

dependencies {
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: rxJavaVersion
    compile group: 'io.netty', name: 'netty-handler', version: nettyVersion
    compile group: 'io.netty', name: 'netty-transport-native-epoll', version: nettyVersion, classifier: 'linux-x86_64'
    compile group: 'com.google.dagger', name: 'dagger', version: daggerVersion
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion

    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile group: 'org.jctools', name: 'jctools-core', version: jcToolsVersion

    apt group: 'com.google.dagger', name: 'dagger-compiler', version: daggerVersion

    testCompile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.0'
    testCompile group: 'junit', name: 'junit', version: junit4Version
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '2.4.1'

    testRuntime group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitVintageVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testRuntime group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
    testRuntime group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
}


def repackagedDir = 'shaded'

shadowJar {
    exclude 'afu.org'
    relocate 'ch.qos.logback', "${repackagedDir}.ch.qos.logback"
    relocate 'com.google', "${repackagedDir}.com.google"
    relocate 'dagger', "${repackagedDir}.dagger"
    relocate 'io.netty', "${repackagedDir}.io.netty"
    relocate 'io.reactivex', "${repackagedDir}.io.reactivex"
    relocate 'org.checkerframework', "${repackagedDir}.org.checkerframework"
    relocate 'org.codehaus', "${repackagedDir}.org.codehaus"
    relocate 'org.reactivestreams', "${repackagedDir}.org.reactivestreams"
    relocate 'org.slf4j', "${repackagedDir}.org.slf4j"
}



publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    /*
    repositories {
        maven {
            mavenCentral()
        }
    }
    */
}
